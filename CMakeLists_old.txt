cmake_minimum_required(VERSION 3.15)
project(SoundCrux VERSION 1.0.0 LANGUAGES CXX)

# Specify C++ standard
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED True)

# Add source directory
file(GLOB_RECURSE SOURCES ${PROJECT_SOURCE_DIR}/src/*.cpp)

# Create library
# STATIC for static library, .lib file
# SHARED for shared library, .dll file
add_library(SoundCrux STATIC ${SOURCES})

# Find RtAudio package
find_package(RtAudio REQUIRED)

# Specify include directories for users of the library
target_include_directories(SoundCrux PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)

# Link dependencies
# PUBLIC makes downstream projects automatically link RtAudio when they link SoundCrux
target_link_libraries(SoundCrux PUBLIC RtAudio::rtaudio)

set_target_properties(SoundCrux PROPERTIES
    ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/$<CONFIG>/lib"
    LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/$<CONFIG>/lib"
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/$<CONFIG>/bin"
)

# Optionally, export the library for use by other projects
install(TARGETS SoundCrux
    EXPORT SoundCruxTargets
    ARCHIVE DESTINATION lib
    LIBRARY DESTINATION lib
    RUNTIME DESTINATION bin
)

install(DIRECTORY ${PROJECT_SOURCE_DIR}/include/ DESTINATION include)

# Export the targets to a script
install(EXPORT SoundCruxTargets
    FILE SoundCruxTargets.cmake
    NAMESPACE SoundCrux::
    DESTINATION lib/cmake/SoundCrux
)

# Create a package configuration file
include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    ${CMAKE_CURRENT_BINARY_DIR}/SoundCruxConfigVersion.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_package_config_file(${CMAKE_CURRENT_SOURCE_DIR}/cmake/SoundCruxConfig.cmake.in
    ${CMAKE_CURRENT_BINARY_DIR}/SoundCruxConfig.cmake
    INSTALL_DESTINATION lib/cmake/SoundCrux
)

install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/SoundCruxConfig.cmake
    ${CMAKE_CURRENT_BINARY_DIR}/SoundCruxConfigVersion.cmake
    DESTINATION lib/cmake/SoundCrux
)
